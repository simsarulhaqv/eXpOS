alias physicalSP S0;
alias sysCallNo S1;
alias newPID S2;
alias newPCB S3;
alias currentPID S4;
alias currentPCB S5;
alias counter S6;
alias waitingPID S7;
alias newPhysicalSP S8;

// get the physical SP
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);
sysCallNo = [physicalSP - 1];

currentPID = (PTBR - 1024) / 8;
currentPCB = READY_LIST + currentPID * 32;

if (sysCallNo == 10 || sysCallNo == 14) then
	counter = 0;
	while (counter < 32) do
		if ([READY_LIST + 32 * counter + 1] == 100 + currentPID) then
			[READY_LIST + 32 * counter + 1] = 1;	
		endif;
		counter = counter + 1;	
	endwhile;
	
	if (sysCallNo == 14) then
		[physicalSP - 2] = 0;
		ireturn;
	endif;
endif;

if (sysCallNo == 10 || sysCallNo == 13) then

	newPID = 0;
	while (newPID < 32) do
		if ([READY_LIST + 32 * newPID + 1] == 1) then
			break;
		endif;
		newPID = newPID + 1;
	endwhile;
		
	
endif;

if (sysCallNo == 10) then

	if (newPID == 32) then
		halt;
	endif;
	
	// close all open instances of files of the current process
	counter = 0;
	while (counter < 8) do
		if ([currentPCB + 15 + counter * 2] != -1) then				
			[FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] = [FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] - 1;
			if ([FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] == 0) then					
				[FILE_TABLE + [currentPCB + 15 + counter * 2] * 2] = -1;
			endif;
			// invalidate the PPOFT by setting fields to -1
			[currentPCB + 15 + counter * 2] = -1;
			[currentPCB + 15 + counter * 2 + 1] = -1;	
		endif;
		counter = counter + 1;
	endwhile;
	
	// state as terminated
	[currentPCB + 1] = 0;
	
	// free the page table entries of the current process
	counter = 0;
	while(counter < PTLR) do

		if ([PTBR + counter * 2] == -1) then
			// already invalid
			counter = counter + 1;
			continue;
		endif;

		if ([PTBR + counter * 2 + 1] == "01" || [PTBR + counter * 2 + 1] == "11") then
			// decrement count in memory free list
			[MEM_LIST + [PTBR + counter * 2]] = [MEM_LIST + [PTBR + counter * 2]] - 1;
		endif;

		if ([PTBR + counter * 2 + 1] == "00" && [PTBR + counter * 2] > 447) then
			[DISK_LIST + [PTBR + counter * 2]] = [DISK_LIST + [PTBR + counter * 2]] - 1;
			// commit the disk free list back into the disk
			store(6, 20);
		endif;

		// invalidating the entry
		[PTBR + counter*2] = -1; 
		[PTBR + counter*2 + 1] = "00";			
		counter = counter + 1;
	endwhile;	

endif;

if (sysCallNo == 13) then	
	waitingPID = [physicalSP - 3];
	if ([READY_LIST + 32 * waitingPID + 1] == 0 || [READY_LIST + 32 * waitingPID + 1] == 2) then
		[physicalSP - 2] = -1;
		ireturn;
	endif;

	[READY_LIST + currentPID * 32 + 1] = 100 + waitingPID;
 
 	// Wait need to store the context of the current process	
	[currentPCB + 2] = BP;
	[currentPCB + 3] = SP - 1;
	// SP -1 because when an interrupt occurs, IP value is stored on the top of the stack. hence the SP value will be SP-1

	// IP of the current process is in the top of the stack
	[currentPCB + 4] = [physicalSP];

	// set PTBR and PTLR
	[currentPCB + 5] = PTBR;
	[currentPCB + 6] = PTLR;

	[currentPCB + 7] = R0;
	[currentPCB + 8] = R1;
	[currentPCB + 9] = R2;
	[currentPCB + 10] = R3;
	[currentPCB + 11] = R4;
	[currentPCB + 12] = R5;
	[currentPCB + 13] = R6;
	[currentPCB + 14] = R7;

	
endif;

if (sysCallNo == 10 || sysCallNo == 13) then

	
	newPCB = READY_LIST + newPID * 32;

	BP = [newPCB + 2];
	SP = [newPCB + 3];	
		
	// set PTBR and PTLR
	PTBR = [newPCB + 5];
	PTLR = [newPCB + 6];
		
	R0 = [newPCB + 7];
	R1 = [newPCB + 8];
	R2 = [newPCB + 9];
	R3 = [newPCB + 10];
	R4 = [newPCB + 11];
	R5 = [newPCB + 12];
	R6 = [newPCB + 13];
	R7 = [newPCB + 14];
		
	SP = SP + 1; // IP to be stored on the top of the stack, hence incrementing
	
	newPhysicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);
	[newPhysicalSP] = [newPCB + 4];
	
	[newPCB + 1] = 2;
	
endif;

if (sysCallNo == 13 || sysCallNo == 14) then
	[physicalSP - 2] = 0;
endif;

ireturn;
